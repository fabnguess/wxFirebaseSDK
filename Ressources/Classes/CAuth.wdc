#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : CAuth
 major_version : 25
 minor_version : 0
 type : 4
 description : ""
 subtype : 0
class :
 identifier : 0x1490bb35003c6435
 internal_properties : BQAAAAUAAAB2/vstTMCJbS/hlxjFxirSqKvCUuv8YxgpWyl7S3iA
 code_elements :
  type_code : 10
  p_codes :
   -
     code : |1+
      // URL de la documentation https://firebase.google.com/docs/reference/rest/auth?hl=fr	
      CONSTANT
      	CONST_AUTH_BASE_URL	= "https://identitytoolkit.googleapis.com/v1/accounts:%1?key=%2"
      FIN	
      
      IAuthProviderService est une Interface
      	procédure Connexion(sUrl est une chaîne):CAuthReponse
      FIN
      
      EProvider est une énumération
      	Google		= "google.com"
      	Facebook	= "facebook.com"
      	GitHub		= "github.com"
      fin
      
      CAuth est une Classe
       	prive
       		m_sFirebaseAPI est une chaîne
       		m_HttpClientReponse est un restRéponse
       		m_sIDToken  est une chaîne
      fin
     type : 131072
  procedures :
   -
     name : Constructeur
     procedure_id : 1481890113716577333
     type_code : 27
     code : |1+
      procédure Constructeur(sFirebaApiKey est une chaîne)
      
      m_sFirebaseAPI = sFirebaApiKey
     type : 589824
   -
     name : Destructeur
     procedure_id : 1481890113716642869
     type_code : 28
     code : |1+
      procédure Destructeur()
     type : 655360
   -
     name : SeConnecter
     procedure_id : 1482560201626089801
     type_code : 12
     code : |1-
      procédure publique SeConnecter(sEmail est une chaîne, sMdp est une chaîne):CAuthReponse
      
      bufCredentialPayload	est un buffer = [
      {
          "email": "[%sEmail%]",
          "password": "[%sMdp%]",
          "returnSecureToken": true
        }
      ]
      
      m_HttpClientReponse = CHttpClient.POST(ConstruitURL("signInWithPassword"), bufCredentialPayload)
      
      
      si ErreurDétectée() _ou_ m_HttpClientReponse.CodeEtat <> 200 ALORS
      	RENVOYER  new CAuthReponse(m_HttpClientReponse, ErreurInfo(errMessage))
      FIN
      
      _DefinirIdToken(m_HttpClientReponse)
      
      RENVOYER _RecupereInfoUtilisateur()
     type : 458752
   -
     name : SeConnecterProvider
     procedure_id : 1658860710682631666
     type_code : 12
     code : |1-
      procédure SeConnecterProvider(clProviderInstance	est IAuthProviderService):CAuthReponse
      
      sUrl				est une chaîne	= ConstruitURL("signInWithIdp")
      
      renvoyer clProviderInstance.Connexion(sUrl)
     type : 458752
   -
     name : CréerUtilisateur
     procedure_id : 1658862634828101156
     type_code : 12
     code : |1-
      procédure CréerUtilisateur(stAuthPayload est STAuthPayload):CAuthReponse
      
      bufCredentialPayload	est un buffer = [
      {
          "email": "[%stAuthPayload.sEmail%]",
          "password": "[%stAuthPayload.sMdp%]",
          "displayName": "[%stAuthPayload.sNomAffichage%]",
          "phoneNumber": "[%stAuthPayload.sNuméroTéléphone%]",
          "photoUrl": "[%stAuthPayload.sPhotoURL%]",
          "returnSecureToken": true
        }
      ]
      
      m_HttpClientReponse = CHttpClient.POST(ConstruitURL("signUp"), bufCredentialPayload)
      
      SI ErreurDétectée() _OU_ m_HttpClientReponse.CodeEtat <> 200 ALORS
      	RENVOYER  new CAuthReponse(m_HttpClientReponse, ErreurInfo(errMessage))
      FIN
      
      _DefinirIdToken(m_HttpClientReponse)
      
      si stAuthPayload.bVerifieEmail ALORS
      	renvoyer _EnvoyerEmailVerification()
      FIN
      
      RENVOYER _RecupereInfoUtilisateur()
     type : 458752
   -
     name : RéinitialiserMotDePasse
     procedure_id : 1658863120159452365
     type_code : 12
     code : |1-
      procédure RéinitialiserMotDePasse(sEmail est une chaîne):CAuthReponse
      
      bufContenu		est un buffer	= [
      	{
      		"requestType": "PASSWORD_RESET",
      		"email": "[%sEmail%]"
      	}
      ]
      
      m_HttpClientReponse = CHttpClient.POST(ConstruitURL("sendOobCode"), bufContenu)
      
      SI ErreurDétectée() _OU_ m_HttpClientReponse.CodeEtat <> 200 ALORS
      	renvoyer new CAuthReponse(m_HttpClientReponse, ErreurInfo(errMessage))
      FIN
      
      RENVOYER  new CAuthReponse(m_HttpClientReponse)
     type : 458752
   -
     name : SupprimerUtilisateur
     procedure_id : 1658864004922921638
     type_code : 12
     code : |1-
      procédure SupprimerUtilisateur()
      
      bufContenu est un buffer = [
      	{"idToken":"[%m_sIDToken%]"}
      ]
      
      m_HttpClientReponse = CHttpClient.POST(ConstruitURL("delete"), bufContenu)
      
      SI ErreurDétectée() _OU_ m_HttpClientReponse.CodeEtat <> 200 ALORS
      	RENVOYER new CAuthReponse(m_HttpClientReponse, ErreurInfo(errMessage))
      FIN
      
      RENVOYER  new CAuthReponse(m_HttpClientReponse)
     type : 458752
   -
     name : _RecupereInfoUtilisateur
     procedure_id : 1658864288390829190
     type_code : 12
     code : |1+
      procédure prive _RecupereInfoUtilisateur():CAuthReponse
      
      bufContenu			est un buffer			= [
      	{"idToken":"[%m_sIDToken%]"}
      ]
      
      m_HttpClientReponse	= CHttpClient.POST(ConstruitURL("lookup"), bufContenu)
      
      SI ErreurDétectée() ALORS
      	RENVOYER  NEW CAuthReponse(m_HttpClientReponse, ErreurInfo(errMessage))
      FIN
      
      RENVOYER  NEW CAuthReponse(m_HttpClientReponse)
      
     type : 458752
   -
     name : ConnexionAnonyme
     procedure_id : 1658864576153704522
     type_code : 12
     code : |1-
      procédure ConnexionAnonyme()
      
      m_HttpClientReponse = CHttpClient.POST(ConstruitURL("signUp"))
      
      
      SI ErreurDétectée() _ou_ m_HttpClientReponse.CodeEtat <> 200 ALORS
      	RENVOYER  new CAuthReponse(m_HttpClientReponse, ErreurInfo(errMessage))
      FIN
      
      RENVOYER _RecupereInfoUtilisateur()
     type : 458752
   -
     name : ConstruitURL
     procedure_id : 1658870954180182977
     type_code : 12
     code : |1+
      procédure privée ConstruitURL(AuthUri est une chaîne):chaine
      
      renvoyer ChaîneConstruit(CONST_AUTH_BASE_URL, AuthUri, m_sFirebaseAPI)
     type : 458752
   -
     name : _EnvoyerEmailVerification
     procedure_id : 1664307815841109200
     type_code : 12
     code : |1+
      procédure privée _EnvoyerEmailVerification():CAuthReponse
      
      bufContenu est un buffer = [
      	{
      		"requestType": "VERIFY_EMAIL",
      		"idToken": "[%m_sIDToken%]"
      	}
      ]
      
      m_HttpClientReponse = CHttpClient.POST(ConstruitURL("sendOobCode"), bufContenu)
      
      SI ErreurDétectée() _OU_ m_HttpClientReponse.CodeEtat <> 200 ALORS
      	RENVOYER  new CAuthReponse(m_HttpClientReponse, ErreurInfo(errMessage))
      FIN
      
      renvoyer new CAuthReponse(m_HttpClientReponse)
      
     type : 458752
   -
     name : _DefinirIdToken
     procedure_id : 1667373318685306736
     type_code : 12
     code : |1-
      procédure privée _DefinirIdToken(oRestReponse est un httpRéponse) <métier>
      
      vJsonResultat est un Variant = JSONVersVariant(oRestReponse.Contenu)
      
      m_sIDToken = vJsonResultat.idToken
     type : 458752
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : BQAAAAUAAAA6ih3UbgNXHwTtiPSFUEj+2fi/m7v4QV2rqidAupM=
  original_name : Classe1
resources :
 string_res :
  identifier : 0x1490bb2d003644f5
  internal_properties : BQAAAAUAAAAnMYFQ1bL/vz9ehh7L22SNNSlIzGTOI8h5F/WtgDNP
custom_note :
 internal_properties : BQAAAAUAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
